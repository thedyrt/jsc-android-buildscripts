apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["${rootDir}/../build/compiled"]
        }
    }

    packagingOptions {
        doNotStrip "**/libjsc.so"
    }
}

dependencies {}

apply plugin: 'maven'

task createAAR(type: Upload) {
    def distDir = "${rootDir}/../dist"

    def revision = project.findProperty("revision") ?: "".replaceAll("\\s", "")
    def i18n = project.findProperty("i18n") ?: ""

    doFirst {
        if (!revision) throw new RuntimeException("expecting --project-prop revision=??? but was empty")
        if (!i18n) throw new RuntimeException("expecting --project-prop i18n=??? but was empty")
    }

    project.group = "org.webkit"
    def artifactName = Boolean.valueOf(i18n) ? "android-jsc-intl" : "android-jsc"
    project.version = "r${revision}"

    configuration = configurations.archives
    repositories.mavenDeployer {
        repository url: "file://${distDir}"
        pom.project {
            name "android-jsc"
            artifactId artifactName
            packaging "aar"
        }
    }
}
